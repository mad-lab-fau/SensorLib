apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.mad-lab-fau'


android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def getGitDate = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'log', '-1', '--date=format:%Y%m%d_%H%M', '--pretty=format:%cd'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28

        def versionMajor = 0
        def versionMinor = 10
        def versionPatch = 0

        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}" + "_" + getGitDate() + "-" + getGitHash()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.6'
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha08'
    implementation 'com.github.hannesa2:MPAndroidChart:3.0.6'
    implementation 'com.xeoh.android:checkboxgroup:1.0.1'

}

tasks.create(name: 'copyApk', type: Copy) {
    from 'build/outputs/aar'
    into '../../Precompiled'
}

tasks.whenTaskAdded { task ->
    if (task.name == 'mockableAndroidJar') {
        task.dependsOn 'copyApk'
    }
}
