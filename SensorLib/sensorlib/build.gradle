apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.github.mad-lab-fau'

android {
    compileSdkVersion 34
    namespace 'de.fau.sensorlib'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def getGitDate = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'log', '-1', '--date=format:%Y%m%d_%H%M', '--pretty=format:%cd'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34

        def versionMajor = 0
        def versionMinor = 18
        def versionPatch = 0

        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}" + "_" + getGitDate() + "-" + getGitHash()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }


}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.7'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0-alpha01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.11.0-beta01'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.xeoh.android:checkboxgroup:1.0.1'

}
